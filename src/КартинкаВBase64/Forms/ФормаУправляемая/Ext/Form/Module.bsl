
#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПутьКФайлуКартинкиНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	ФорматыИзображений = Новый Массив;
	ФорматыИзображений.Добавить("PNG|*.png");
	ФорматыИзображений.Добавить("JPEG|*.jpg;*.jpeg");
	ФорматыИзображений.Добавить("GIF|*.gif");
	ФорматыИзображений.Добавить("BMP|*.bmp");
	
	Фильтр = СтрСоединить(ФорматыИзображений, "|");
	
	ДиалогФыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогФыбораФайла.Фильтр                  = Фильтр;
	ДиалогФыбораФайла.Заголовок               = "Выберите файл картинки для преобразования:";
	ДиалогФыбораФайла.ПредварительныйПросмотр = Истина;
	ДиалогФыбораФайла.МножественныйВыбор      = Ложь;
	
	ОбработчикЗавершения = Новый ОписаниеОповещения("ПутьКФайлуКартинкиЗавершениеВыбора", ЭтаФорма);
	ДиалогФыбораФайла.Показать(ОбработчикЗавершения);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Преобразовать(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ЭтаФорма.Объект.РезультатПреобразования = ТекстТегаИзображения();
	
	ВыделитьТекстТегаИзображения();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает готовый текст тега <img> с источником-двоичными данными,
// закодированными в Base64.
//
// Возвращаемое значение:
//  Строка - См. описание функции.
//
&НаКлиенте
Функция ТекстТегаИзображения()
	
	КартинкаДляПреобразования = Новый Картинка(ЭтаФорма.Объект.ПутьКФайлуКартинки);
	
	ДанныеКартинкиBase64 = Base64Строка(КартинкаДляПреобразования.ПолучитьДвоичныеДанные());
	
	// Правильность формата не проверяется намеренно,
	// пользователь должен об этом позаботиться сам.
	ФорматФайла = НРег(КартинкаДляПреобразования.Формат());
	
	ТекстТега = СтрШаблон("<img src=""data:image/%1;base64,%2"">", ФорматФайла, ДанныеКартинкиBase64);
	
	Возврат ТекстТега;
	
КонецФункции

// См. имя процедуры.
//
&НаКлиенте
Процедура ВыделитьТекстТегаИзображения()
	
	// Обновляем отображение, иначе может оказаться,
	// что пытаемся выделить то, чего пока нет на форме.
	ЭтаФорма.ОбновитьОтображениеДанных(ЭтаФорма.Элементы.РезультатПреобразования);
	
	ЭтаФорма.ТекущийЭлемент = ЭтаФорма.Элементы.РезультатПреобразования;
	
	НачальнаяПозиция = 1;
	КонечнаяПозиция  = СтрДлина(ЭтаФорма.Объект.РезультатПреобразования) + 1;
	ЭтаФорма.Элементы.РезультатПреобразования.УстановитьГраницыВыделения(НачальнаяПозиция, КонечнаяПозиция);
	
КонецПроцедуры

&НаКлиенте
Процедура ПутьКФайлуКартинкиЗавершениеВыбора(Знач ВыбранныеФайлы, Знач ДополнительныеПараметры) Экспорт
	
	Если (ВыбранныеФайлы <> Неопределено) Тогда
		ЭтаФорма.Объект.ПутьКФайлуКартинки = ВыбранныеФайлы[0];
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
